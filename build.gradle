import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
}

group 'com.amazon.corretto.hotpatch'
version = file('version.txt').text.trim()
repositories {
    mavenCentral()
}

jar {
    archiveFileName.set('Log4jHotPatch.jar')
    manifest {
        from 'src/main/resources/MANIFEST.MF'
        attributes (
                'Implementation-Version': archiveVersion.get(),
                'Build-Jdk': System.properties.getProperty('java.version'),
                'Created-By': "Gradle ${getGradle().getGradleVersion()}"
        )
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

dependencies {
    if (Jvm.current().javaVersion.java8) {
        compileOnly files(Jvm.current().toolsJar)
    }
}

task runShellTests(type: Exec) {
    dependsOn jar
    commandLine 'sh', "${rootProject.projectDir}/build-tools/bin/run_tests.sh", jar.archiveFile.get(), Jvm.current().getJavaHome().toString(), '--summary'
}

test.dependsOn runShellTests
